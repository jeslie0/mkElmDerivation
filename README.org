#+title: mkElmDerivation
This project is an attempt to provide a way to package elm applications with nix, with less user input required than the standard [[https://github.com/cachix/elm2nix][elm2nix]]. This project is based heavily on elm2nix and wouldn't be possible without it. There should be no need to generate any nix files using elm2nix, and no updates required when installing a new elm package. The nix expression is generated on the fly from the given =elm.json= file.

The flake provides an overlay containing the function =mkElmDerivation=, which will build an elm project. The function takes the following attribute set as an input:

#+begin_src nix
{ # The name of the elm project.
  pname

  # The version of the elm project.
, version

  # The base directory of your elm project (most likely ./.).
, src

  # Optional: The elm projects elm.json file. Will default to
  # ${src}/elm.json
, elmJson ? "${src}/elm.json"

  # Optional: The directory containing the .elm files to
  # compile. Currently, we only support using one directory.
, srcdir ? with builtins; "${src}/${head ((fromJSON (readFile elmJson))."source-directories")}"

  # Optional: The version of elm used. Read from elm.json file.
, elmVersion ? with builtins; (fromJSON (readFile elmJson))."elm-version"

  # Optional: The files in srcdir to compile.
, targets ? [ "Main" ]

  # Optional: Should the outputted files be JavaScript or HTML?
, outputJavaScript ? false
}
#+end_src

Here is an example that uses the overlay:

#+name: Overlay example
#+begin_src nix
# flake.nix
{
  description = "An elm derivation using the mkElmDerivation overlay.";

  inputs = {
    nixpkgs.url = github:nixos/nixpkgs/nixos-unstable;
    mkElmDerivation.url = github:jeslie0/mkElmDerivation;
  };

  outputs = { self, nixpkgs, mkElmDerivation }:
    let
      system = "x86_64-linux";
      pkgs = import nixpkgs {
        overlays = [ mkElmDerivation.overlay.${system} ];
        inherit system;
      };
    in
      {
        packages.${system}.default = pkgs.mkElmDerivation {
          pname = "elm-app";
          version = "0.1.0";
          src = ./.;
          outputJavaScript = true;
        };
      };
}
#+end_src
* Versions
This project currently supports version 0.19.1 of Elm.
* Updates
Currently, a GitHub action is set to run at 0000 every Sunday. This will update the three JSON files stored in the [[file:mkElmDerivation/][mkElmDerivation directory]].
